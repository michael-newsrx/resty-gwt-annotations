plugins {
    id 'java-library'
    id "idea"
    id "org.jetbrains.gradle.plugin.idea-ext"
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

java.sourceCompatibility = JavaVersion.VERSION_17
java.toolchain.languageVersion.set(JavaLanguageVersion.of(21))
java.withSourcesJar()
sourcesJar {
    from(sourceSets.main.allSource)
    from(tasks.named("compileJava").map { it.outputs.files })
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}
jar {
    from sourceSets.main.allSource // include sources for use by gwt
    from(tasks.named("compileJava").map { it.outputs.files })
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}
sourcesJar.dependsOn compileJava
processResources.dependsOn compileJava

repositories {
    mavenCentral()
    google()
    maven { url 'https://packages.jetbrains.team/maven/p/ij/intellij-dependencies/' }
    maven { url 'https://jitpack.io' }
    maven { url "https://github.com/psiegman/mvn-repo/raw/master/releases" }
}

dependencies {
    compileOnly("org.gwtproject:gwt-user:2.12.2")
    compileOnly('com.google.elemental2:elemental2-dom:1.3.2')
    compileOnly(project(':resty-gwt'))
}

tasks.register('create-dirs') {
    doLast {
        sourceSets*.java.srcDirs*.each { it.mkdirs() }
        sourceSets*.resources.srcDirs*.each { it.mkdirs() }
    }
}

test {
    useTestNG()
    testLogging.showStandardStreams = true
}
